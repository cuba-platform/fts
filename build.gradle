/*
 * Copyright (c) 2011 Haulmont Technology Ltd. All Rights Reserved.
 * Haulmont Technology proprietary and confidential.
 * Use is subject to license terms.
 */

def defaultVersion = '4.1'

def BUILD_VERSION = 'buildVersion'
def BUILD_VCS_NUMBER = 'buildVcsNumber'

allprojects {
    ext.artifactGroup = 'com.haulmont.fts'
    ext.artifactVersion = rootProject.hasProperty(BUILD_VERSION) ? 
                                    rootProject[BUILD_VERSION].replace('-SNAPSHOT', '') : defaultVersion
    ext.isSnapshot = !rootProject.hasProperty(BUILD_VERSION) || rootProject[BUILD_VERSION].endsWith('-SNAPSHOT')
    //ext.tomcatDir = rootDir.absolutePath + '/../tomcat'
}

buildscript {
	def cubaPluginVersion = '4.1-SNAPSHOT'	
	def CUBA_PLUGIN_VERSION = 'cubaPluginVersion'

	cubaPluginVersion = rootProject.hasProperty(CUBA_PLUGIN_VERSION) ? rootProject[CUBA_PLUGIN_VERSION] : cubaPluginVersion
	
	ext.cubaPlugin = [group: 'com.haulmont.gradle', name: 'cuba-plugin', version: cubaPluginVersion]
	
    repositories {
        mavenLocal()
        maven {
            credentials {
                username System.getenv('HAULMONT_REPOSITORY_USER')
                password System.getenv('HAULMONT_REPOSITORY_PASSWORD')
            }
            url "http://repository.haulmont.com:8587/nexus/content/groups/work"
        }
    }
    dependencies {
        classpath cubaPlugin
    }
}

apply(plugin: 'idea')
apply(plugin: 'cuba')

def globalModule = project(':fts-global')
def coreModule = project(':fts-core')
def webModule = project(':fts-web')

def baseVersion = artifactVersion + (isSnapshot ? '-SNAPSHOT' : '')


configure([globalModule, coreModule, webModule]) {
    apply(plugin: 'java')
    apply(plugin: 'idea')
    apply(plugin: 'maven')
    apply(plugin: CubaPlugin)

    dependencies {
        compile(group: 'com.haulmont.cuba', name: 'cuba-global', version: baseVersion)
        testCompile(group: 'junit', name: 'junit', version: '4.5')
    }

    task sourceJar(type: Jar) {
        from file('src')
        classifier = 'sources'
    } 

    artifacts {
        archives sourceJar
    }
}

configure(coreModule) {
    dependencies {
        compile(globalModule)
        compile(group: 'com.haulmont.cuba', name: 'cuba-core', version: baseVersion)
        compile(group: 'org.apache.lucene', name: 'lucene-core', version: '3.0.2')
        compile(group: 'org.apache.tika', name: 'tika-parsers', version: '0.9') {
            exclude(group: 'org.apache.poi', module: 'poi')
        }
        compile(group: 'org.apache.pdfbox', name: 'pdfbox', version: '1.7.1')

        testCompile(group: 'com.haulmont.cuba', name: 'cuba-core', version: baseVersion, classifier: 'tests')
    }
}

configure(webModule) {
    dependencies {
        compile(globalModule)
        compile(group: 'com.haulmont.cuba', name: 'cuba-web', version: baseVersion)
    }
}
